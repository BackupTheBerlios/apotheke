dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(apotheke, 0.3)
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

dnl ******************************
dnl pkg-config checks
dnl ******************************
PKG_CHECK_MODULES(APOTHEKE, glib-2.0 gdk-pixbuf-2.0 gnome-vfs-2.0 gtk+-2.0 libnautilus >= 2.2.1 libgnome-2.0 gconf-2.0 libglade-2.0)
AC_SUBST(APOTHEKE_CFLAGS)
AC_SUBST(APOTHEKE_LIBS)


dnl ******************************
dnl gconf stuff
dnl ******************************
AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2


dnl ******************************
dnl libfam check
dnl ******************************
FAM_MISSING_WARNING="Apotheke depends on FAM to provide notification when files are altered (either through filesystem polling, or a kernel notification mechanism). If Apotheke is built without FAM support, directories viewed with Apotheke will not remain in synch with the actual filesystem when they are altered by external processes. FAM is available from http://oss.sgi.com/projects/fam/. A patch to add Linux Kernel 2.4 directory notify support to FAM (highly desirable) is available from http://people.redhat.com/alexl/files/" 
FAM_LIBS=
AC_CHECK_LIB(fam, FAMOpen,
  [AC_CHECK_HEADERS(fam.h,
    [AC_DEFINE(HAVE_LIBFAM)
     FAM_LIBS="-lfam"],
    AC_MSG_WARN(*** FAM support will not be built (header files not found) $FAM_MISSING_WARNING ***))],
  AC_MSG_WARN(*** FAM support will not be built (FAM library not found) $FAM_MISSING_WARNING ***))
AC_SUBST(FAM_LIBS)


dnl *******************************
dnl Internationalization
dnl ******************************* 
GETTEXT_PACKAGE=apotheke
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE")
AC_SUBST(GETTEXT_PACKAGE)

dnl Add the languages which your application supports here.
ALL_LINGUAS="de"
AM_GLIB_GNU_GETTEXT

dnl *******************************
dnl Output
dnl ******************************* 
AC_OUTPUT([
Makefile
src/Makefile
icons/Makefile
po/Makefile.in
apotheke.spec])
